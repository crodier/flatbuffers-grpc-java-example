plugins {
    id 'java-library'
}

task flatc(type: Exec) {
    workingDir = projectDir.toString()
    commandLine = projectDir.toString() + "/flatc"
    args = ["--grpc", "-j", "-o", "${buildDir}/generated/sources/flatbuffers/java/main/", "${projectDir}/src/main/flatbuffers/StupidServer.fbs"]
}

sourceSets {
    main {
        java {
            srcDir("${buildDir}/generated/sources/flatbuffers/java/main/")
        }
    }
}

compileJava{
    dependsOn(flatc)
}
ext{
    // flatbuffersVersion = '2.0.3'
    flatbuffersVersion = '23.3.3'
    grpcVersion = '1.48.1'
}

dependencies {
    api 'javax.annotation:javax.annotation-api:1.3.2' //used by generated classes
    api "com.google.flatbuffers:flatbuffers-java:$flatbuffersVersion" //used by generated classes
    api "com.google.flatbuffers:flatbuffers-java-grpc:$flatbuffersVersion" //used by generated classes
    api "io.grpc:grpc-services:$grpcVersion" //used by generated classes

    api "io.grpc:grpc-netty:$grpcVersion" //used by client and server
}

repositories {
    // using this commit of the java flatbuffers lib is
    // an easy way to get 23.3.3 jar into this build.
    // This is no loger on maven central, seems like a build issue with flatbuffers
    // latest and well known, flatbuffers-java release, as of 4/29/2023
    // docs:  https://docs.gradle.org/current/userguide/declaring_repositories.html
    flatDir {
        dirs '../maven-repo-lib'
    }

    // useful for building locally
    // mavenLocal()
    google()

    // good default
    mavenCentral()
}